plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'xss.it.icons.demo'
version '1.0.0'
project.description='Collection of Icons'

repositories {
    mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule='IconsDemo'
    mainClass = 'xss.it.icons.demo.Demo'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}


jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    launcher {
        name = 'Font Icons'
        jvmArgs = ['-Xms64m','-Xmx1500m','-XX:+UseG1GC','-XX:MaxGCPauseMillis=200','-Djdk.gtk.version=2']
    }
    forceMerge('log4j-api')
    jpackage {
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth Â© 2023',
                '--app-version',project.version,
                '--vendor', 'XSSIT by Geo'
        ]

        //Windows
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()){
            imageOptions += ['--icon', 'src/main/resources/xss/it/icons/demo/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }

    }
}
